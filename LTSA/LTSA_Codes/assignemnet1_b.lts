/*
constant properties for pilot numer, tug number, required tugs for docking, undocking
and the maximum space of wait zones.
*/
const PIL_NUM = 2
const TUG_NUM = 5
const TUG_DOCKING = 3
const TUG_UNDOCKING = 2
const WAIT_ZONE_MAX = 1

range WAIT_ZONE_RANGE = 0..WAIT_ZONE_MAX

// The pilot process
PILOT = ( get_ship -> travel -> get_tugs_docking -> docking 
		-> unload -> get_tugs_undocking -> undocking -> travel -> leave_ship -> PILOT ).

// The producer and consumer process, actions put and get mean the a new ship arrives or departure
PRODUCER = ( put -> PRODUCER ).
CONSUMER = ( get -> CONSUMER ).

// The tugs process holds all requests to get tugs or release tugs before docking and after undocking
TUGS( N=5 ) = TUG[5],
TUG[v:0..TUG_NUM] = ( when ( v>= TUG_DOCKING ) acquire_docking -> TUG[v-TUG_DOCKING] | when( v<= TUG_NUM - TUG_DOCKING) release_docking -> TUG[v+TUG_DOCKING] 
				|  when ( v>= TUG_UNDOCKING ) acquire_undocking -> TUG[v-TUG_UNDOCKING] | when( v<= TUG_NUM - TUG_UNDOCKING) release_undocking -> TUG[v+TUG_UNDOCKING])
/{get_tugs_docking/acquire_docking, unload/release_docking, get_tugs_undocking/acquire_undocking, leave_ship/release_undocking}.

// arrive_zone process: only when the number of ships in the zone is less then the MAX value, ships could be in
//						also it's impossible to depart a ship if there are no ships in the zone
// depart_zone use the same method to handle arrive and depart request
ARRIVE_ZONE( N=0 ) = ARRIVE[0],
ARRIVE[v:WAIT_ZONE_RANGE] = ( when ( v< WAIT_ZONE_MAX ) arrive_arrive -> ARRIVE[v+1] | when( v>0 ) ship_leave -> ARRIVE[v-1])
/{put/arrive_arrive,get_ship/ship_leave}.

DEPART_ZONE( N=0 ) = DEPART[0],
DEPART[v:WAIT_ZONE_RANGE] = ( when (v< WAIT_ZONE_MAX ) arrive_depart -> DEPART[v+1] | when( v>0 ) ship_leave -> DEPART[v-1])
/{get/ship_leave,leave_ship/arrive_depart}.  

||SIMULATOR = ( a:PILOT||b:PILOT||{a,b}::TUGS(5)||ARRIVE_ZONE(0)||DEPART_ZONE(0)||PRODUCER||CONSUMER ).
//||SIMULATOR = ( PILOT||TUGS(5)||ARRIVE_ZONE(0)||DEPART_ZONE(0)||PRODUCER||CONSUMER).